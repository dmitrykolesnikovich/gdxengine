apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = "com.kerberjg.gdxstudio"
version = "0.0.1"

ext {
	projectName = name
	
	// Versions of dependencies
	gdxVersion = "1.9.5"
	ashleyVersion = "1.7.3"
}

sourceSets {
	main.java.srcDirs = ["src"]
	test.java.srcDirs = ["test"]
}

repositories {
    jcenter()
    
    mavenLocal();
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
	// libGDX
	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
	compile "com.badlogicgames.ashley:ashley:$ashleyVersion"
	
	// Test dependencies
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task release(dependsOn: [jar, sourcesJar, javadocJar]) {}

task writePom << {
    pom {
        project {
            groupId = group
            artifactId = name
            version = version
        
            inceptionYear '2015'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

eclipse {
	jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}